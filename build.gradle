buildscript {

    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url 'https://oss.jfrog.org/oss-snapshot-local/'
        }
    }

    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1.1-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'me.champeau.gradle.antlr4'


sourceCompatibility = 1.7

dependencies {
    compile 'org.antlr:antlr4:4.3'
    compile 'commons-cli:commons-cli:1.2'
    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.0.4'
    compile 'com.yuvalshavit:antlr-denter:1.1'

    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

mainClassName = 'Main'

jar {
    manifest {
        attributes 'Main-Class' : mainClassName
    }
}

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += antlr4.output

// add antlr4 to classpath
configurations {
    compile.extendsFrom antlr4
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full" // default is "short"
    }
}

